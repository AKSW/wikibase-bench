# Query 2
# Politicians who died of cancer (of any type)
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Politicians_who_died_of_cancer_.28of_any_type.29
# 4) Named Graphs
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdo: <http://www.wikidata.org/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p: <http://www.wikidata.org/prop/>
SELECT ?politician ?cause ?politician_label ?cause_of_death_label {
  GRAPH ?g1 { ?politician p:P106 wd:Q82955 . }
  GRAPH ?g2 { ?politician p:P509 ?cause . }
  ?cause p:P279* wd:Q12078 .
  OPTIONAL  {
    ?politician rdfs:label ?politician_label . filter (lang(?politician_label) = "en") .
    ?cause rdfs:label ?cause_of_death_label . filter (lang(?cause_of_death_label) = "en") .
  }
}
