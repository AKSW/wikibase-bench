# Query 8
# Population in Europe after 1960
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Population_in_Europe_after_1960
# 4) Named Graphs
PREFIX wikibase: <http://wikiba.se/ontology-beta#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdo: <http://www.wikidata.org/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT  ?objectLabel ?year ?population
WHERE
{
	GRAPH ?g1 { ?object p:P31 wd:Q185441 . }
	GRAPH ?populationStatement {
    ?object p:P1082 ?population .
    ?populationStatement p:P585 [ wikibase:timeValue ?date ] .
  }
	OPTIONAL { ?object rdfs:label ?objectLabel . FILTER(lang(?objectLabel)='en') }
  BIND (xsd:integer(strBefore(str(?date),"-")) as ?year)
  FILTER (?year >= 1960)
}
ORDER BY ?objectLabel ?year
