# Query 6
# Academy award data
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Academy_award_data
# 3) singleton properties
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX p: <http://www.wikidata.org/prop/>
SELECT ?human ?humanLabel ?awardEditionLabel ?awardLabel ?awardWork ?awardWorkLabel ?director ?directorLabel ?time
WHERE
{
	{
		SELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time) WHERE {
			?award ?sing1 wd:Q19020 . ?sing1 rdf:singletonPropertyOf p:P31 .  # All items that are instance of(P31) of Academy awards (Q19020)
			{
				?human ?awardStat ?award . ?awardStat rdf:singletonPropertyOf p:P166 .  # Humans with an awarded(P166) statement  ... that has any of the values of ?award
				?awardStat p:P805 ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
				?awardStat p:P1686 ?awardWork . # The work they have been awarded for
				?human ?sing2 wd:Q5 . ?sing2 rdf:singletonPropertyOf p:P31 . # Humans
			} UNION {
				?awardWork ?awardStat ?award . ?awardStat rdf:singletonPropertyOf p:P166 . # ... with an awarded(P166) statement ... that has any of the values of ?award
				?awardStat p:P805 ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
				?awardWork ?sing2 wd:Q11424 . ?sing2 rdf:singletonPropertyOf p:P31 . # Films
			}
			OPTIONAL {
				?awardEdition ?sing3 ?time . ?sing3 rdf:singletonPropertyOf p:585 . # the "point of time" of the Academy Award
				?awardWork ?sing4 ?director . ?sing4 rdf:singletonPropertyOf p:57 .
			}
		}
		GROUP BY ?awardWork ?award # We only want every movie once for a category (a 'random' person is selected)
	}


	OPTIONAL { ?human rdfs:label ?humanLabel . FILTER (lang(?humanLabel) = "en") }
	OPTIONAL { ?awardEdition rdfs:label ?awardEditionLabel . FILTER (lang(?awardEditionLabel) = "en") }
	OPTIONAL { ?award rdfs:label ?awardLabel . FILTER (lang(?awardLabel) = "en") }
	OPTIONAL { ?awardWork rdfs:label ?awardWorkLabel . FILTER (lang(?awardWorkLabel) = "en") }
	OPTIONAL { ?director rdfs:label ?directorLabel . FILTER (lang(?directorLabel) = "en") }
}
ORDER BY DESC(?time)
