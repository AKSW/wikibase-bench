# Query 6
# Academy award data
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Academy_award_data
# 3) singleton properties
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX p: <http://www.wikidata.org/prop/>
SELECT ?human ?humanLabel ?awardEditionLabel ?awardLabel ?awardWork ?awardWorkLabel ?director ?directorLabel ?time
WHERE
{
	{
		SELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time)
		WHERE {
			?award ?sing1 wd:Q19020 . ?sing1 rdf:singletonPropertyOf p:P31 .
			{
				?human ?awardStat ?award . ?awardStat rdf:singletonPropertyOf p:P166 .
				?awardStat p:P805 ?awardEdition .
				?awardStat p:P1686 ?awardWork .
				?human ?sing2 wd:Q5 . ?sing2 rdf:singletonPropertyOf p:P31 .
			} UNION {
				?awardWork ?awardStat ?award . ?awardStat rdf:singletonPropertyOf p:P166 .
				?awardStat p:P805 ?awardEdition .
				?awardWork ?sing2 wd:Q11424 . ?sing2 rdf:singletonPropertyOf p:P31 .
			}
			OPTIONAL {
				?awardEdition ?sing3 ?time . ?sing3 rdf:singletonPropertyOf p:585 .
				?awardWork ?sing4 ?director . ?sing4 rdf:singletonPropertyOf p:57 .
			}
		}
		GROUP BY ?awardWork ?award
	}


	OPTIONAL { ?human rdfs:label ?humanLabel . FILTER (lang(?humanLabel) = "en") }
	OPTIONAL { ?awardEdition rdfs:label ?awardEditionLabel . FILTER (lang(?awardEditionLabel) = "en") }
	OPTIONAL { ?award rdfs:label ?awardLabel . FILTER (lang(?awardLabel) = "en") }
	OPTIONAL { ?awardWork rdfs:label ?awardWorkLabel . FILTER (lang(?awardWorkLabel) = "en") }
	OPTIONAL { ?director rdfs:label ?directorLabel . FILTER (lang(?directorLabel) = "en") }
}
ORDER BY DESC(?time)
