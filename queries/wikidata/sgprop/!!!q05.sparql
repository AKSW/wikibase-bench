# Query 5
# Number of ministers who are themselves children of a minister, per country
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Number_of_ministers_who_are_themselves_children_of_a_minister.2C_per_country
# 3) singleton properties
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?cc (COUNT(DISTINCT ?child) AS ?number)
WHERE
{
  ?child ?sing1 wd:Q5 . ?sing1 rdf:singletonProperty wd:P31 .  	# Looking for real humans and not fictional ones
  ?child ?sing2 ?position . ?sing2 rdf:singletonProperty wd:P39 .
  ?position ?sing3 wd:Q83307 . ?sing3 rdf:singletonProperty wd:P279 .  # !!should be wd:P279* which is not possible!! 
  {  
     ?child ?sing4 ?parent . ?sing4 rdf:singletonProperty wd:P22 .
	 ?parent ?sing5 ?pposition . ?sing5 rdf:singletonProperty wd:P39 .
	 ?pposition ?sing6 wd:Q83307 . ?sing6 rdf:singletonProperty wd:P279 .  # !!should be wd:P279* which is not possible!! 
  } UNION { 
     ?child ?sing4 ?parent . ?sing4 rdf:singletonProperty wd:P25.
	 ?parent ?sing5 ?pposition . ?sing5 rdf:singletonProperty wd:P39 .
	 ?pposition ?sing6 wd:Q83307 . ?sing6 rdf:singletonProperty wd:P279 .  # !!should be wd:P279* which is not possible!! 
  }
  
  ?child ?sing7 ?country . ?sing7 rdf:singletonProperty wd:P27 . 
  ?country ?sing8 ?cc . ?sing8 rdf:singletonProperty wd:P901 . 
}
GROUP BY ?cc 
ORDER BY DESC(?number)