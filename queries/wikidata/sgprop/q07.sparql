# Query 7
# List of popes
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#List_of_popes
# 3) singleton properties
PREFIX wikibase: <http://wikiba.se/ontology-beta#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX p: <http://www.wikidata.org/prop/>
SELECT ?link ?linkLabel ?picture ?age
WHERE
{
	?link ?sing1 wd:Q5 . ?sing1 rdf:singletonPropertyOf p:P31 .
	?link ?guidPositionHeldPope wd:Q19546 .
	?guidPositionHeldPope rdf:singletonPropertyOf p:P39 ; p:P580 [ wikibase:timeValue ?startTime ] .
	OPTIONAL {
		?link ?sing2 [ wikibase:timeValue ?dateOfBirth ] . ?sing2 rdf:singletonPropertyOf p:P569 .
		BIND (xsd:integer(strBefore(str(?dateOfBirth),"-")) as ?yearOfBirth)
	}
	OPTIONAL { ?link ?sing3 ?picture . ?sing3 rdf:singletonPropertyOf p:P18 . }
	OPTIONAL {
		?link ?sing4 [ wikibase:timeValue ?dateOfDeath ] . ?sing4 rdf:singletonPropertyOf p:P570 .
		BIND (xsd:integer(strBefore(str(?dateOfDeath),"-")) as ?yearOfDeath)
	}

	BIND(?yearOfDeath-?yearOfBirth as ?age )
	OPTIONAL { ?link rdfs:label ?linkLabel . FILTER(lang(?linkLabel) = 'en')}
}
ORDER BY DESC(?startTime)
