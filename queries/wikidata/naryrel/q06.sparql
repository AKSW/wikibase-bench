# Query 6
# Academy award data
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Academy_award_data
# 1) n-ary predicates
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?human ?humanLabel ?awardEditionLabel ?awardLabel ?awardWork ?awardWorkLabel ?director ?directorLabel ?time 
WHERE 
{
	{
		SELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time) WHERE {
			?award wd:P31s [ wd:P31v  wd:Q19020 ] .			# All items that are instance of(P31) of Academy awards (Q19020)
			{
				?human wd:P166s ?awardStat .              # Humans with an awarded(P166) statement
				?awardStat wd:P166v ?award .     	 # ... that has any of the values of ?award
				?awardStat wd:P805q ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
				?awardStat wd:P1686q ?awardWork . # The work they have been awarded for
				?human wd:P31s [ wd:P31v wd:Q5 ] . 				# Humans
			} UNION {
				?awardWork wd:P166s ?awardStat . # ... with an awarded(P166) statement
				?awardStat wd:P166v ?award .     	 # ... that has any of the values of ?award
				?awardStat wd:P805q ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
				?awardWork wd:P31s [ wd:P31v wd:Q11424 ] . # Films
			}
			OPTIONAL {
				?awardEdition wd:P585s [ wd:P585v ?time ] . # the "point of time" of the Academy Award
				?awardWork wd:P57s [ wd:P57v ?director ] .
			}
		}
		GROUP BY ?awardWork ?award # We only want every movie once for a category (a 'random' person is selected)
	}

	
	OPTIONAL { ?human rdfs:label ?humanLabel . FILTER (lang(?humanLabel) = "en") }
	OPTIONAL { ?awardEdition rdfs:label ?awardEditionLabel . FILTER (lang(?awardEditionLabel) = "en") }
	OPTIONAL { ?award rdfs:label ?awardLabel . FILTER (lang(?awardLabel) = "en") }
	OPTIONAL { ?awardWork rdfs:label ?awardWorkLabel . FILTER (lang(?awardWorkLabel) = "en") }
	OPTIONAL { ?director rdfs:label ?directorLabel . FILTER (lang(?directorLabel) = "en") }
}
ORDER BY DESC(?time)