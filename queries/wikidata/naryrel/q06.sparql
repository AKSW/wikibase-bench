# Query 6
# Academy award data
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Academy_award_data
# 1) n-ary predicates
PREFIX wikibase: <http://wikiba.se/ontology-beta#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
SELECT ?human ?humanLabel ?awardEditionLabel ?awardLabel ?awardWork ?awardWorkLabel ?director ?directorLabel ?time
WHERE
{
	{
		SELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time) WHERE {
			?award p:P31 [ ps:P31  wd:Q19020 ] .
			{
				?human p:P166 ?awardStat .
				?awardStat ps:P166 ?award .
				?awardStat p:P805 ?awardEdition .
				?awardStat p:P1686 ?awardWork .
				?human p:P31 [ ps:P31 wd:Q5 ] .
			} UNION {
				?awardWork p:P166 ?awardStat .
				?awardStat ps:P166 ?award .
				?awardStat p:P805 ?awardEdition .
				?awardWork p:P31 [ ps:P31 wd:Q11424 ] .
			}
			OPTIONAL {
				?awardEdition p:P585 [ ps:P585 [ wikibase:timeValue ?time ] ] .
				?awardWork p:P57 [ ps:P57 ?director ] .
			}
		}
		GROUP BY ?awardWork ?award
	}
	OPTIONAL { ?human rdfs:label ?humanLabel . FILTER (lang(?humanLabel) = "en") }
	OPTIONAL { ?awardEdition rdfs:label ?awardEditionLabel . FILTER (lang(?awardEditionLabel) = "en") }
	OPTIONAL { ?award rdfs:label ?awardLabel . FILTER (lang(?awardLabel) = "en") }
	OPTIONAL { ?awardWork rdfs:label ?awardWorkLabel . FILTER (lang(?awardWorkLabel) = "en") }
	OPTIONAL { ?director rdfs:label ?directorLabel . FILTER (lang(?directorLabel) = "en") }
}
ORDER BY DESC(?time)
