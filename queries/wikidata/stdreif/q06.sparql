# Query 6
# Academy award data
# Original source: https://www.mediawiki.org/wiki/Wikibase/Indexing/SPARQL_Query_Examples#Academy_award_data
# 2) RDF reification
PREFIX wikibase: <http://wikiba.se/ontology-beta#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX p: <http://www.wikidata.org/prop/>
SELECT ?human ?humanLabel ?awardEditionLabel ?awardLabel ?awardWork ?awardWorkLabel ?director ?directorLabel ?time
WHERE
{
	{
		SELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time) WHERE {
			[] rdf:subject ?award ; rdf:predicate p:P31 ; rdf:object wd:Q19020 . # All items that are instance of(P31) of Academy awards (Q19020)
			{
				?awardStat rdf:subject ?human ; rdf:predicate p:P166 ; rdf:object ?award . # Humans with an awarded(P166) statement ... that has any of the values of ?award
				?awardStat p:P805 ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
				?awardStat p:P1686 ?awardWork . # The work they have been awarded for
				[] rdf:subject ?human ; rdf:predicate p:P31 ; rdf:object wd:Q5 . # Humans
			} UNION {
				?awardStat rdf:subject ?awardWord ; rdf:predicate p:P166 ; rdf:object ?award . # ... with an awarded(P166) statement ... that has any of the values of ?award
				?awardStat p:P805 ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
				[] rdf:subject ?awardWork ; rdf:predicate p:P31 ; rdf:object wd:Q11424 . # Films
			}
			OPTIONAL {
				[] rdf:subject ?awardEdition ; rdf:predicate p:P585 ; rdf:object [ wikibase:timeValue ?time ] . # the "point of time" of the Academy Award
				[] rdf:subject ?awardWork ; rdf:predicate p:P57 ; rdf:object ?director .
			}
		}
		GROUP BY ?awardWork ?award # We only want every movie once for a category (a 'random' person is selected)
	}


	OPTIONAL { ?human rdfs:label ?humanLabel . FILTER (lang(?humanLabel) = "en") }
	OPTIONAL { ?awardEdition rdfs:label ?awardEditionLabel . FILTER (lang(?awardEditionLabel) = "en") }
	OPTIONAL { ?award rdfs:label ?awardLabel . FILTER (lang(?awardLabel) = "en") }
	OPTIONAL { ?awardWork rdfs:label ?awardWorkLabel . FILTER (lang(?awardWorkLabel) = "en") }
	OPTIONAL { ?director rdfs:label ?directorLabel . FILTER (lang(?directorLabel) = "en") }
}
ORDER BY DESC(?time)
